/*
 * TIATracker music player
 * Copyright 2016 Andre "Kylearan" Wichmann
 * Website: https://bitbucket.org/kylearan/tiatracker
 * Email: andre.wichmann@gmx.de
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/* =====================================================================
 * TIATracker Player
 * =====================================================================
 */

// PLANNED PLAYER VARIANTS:
// - RAM, speed, player ROM: c0/c1 patterns have same length
// - RAM: Pack 2 values (out of cur_pat_index, cur_note_index, envelope_index)
//       into one and use lsr/asl to unpack them, allowing only ranges of
//       16/16 or 32/8 for them, depending on number of patterns, max
//       pattern size and max ADSR size
// - ROM: Check if tt1_SequenceTable can hold ptrs directly without indexing
//       tt1_PatternPtrLo/Hi. Can be smaller if not many patterns get repeated
//       (saves table and decode routine)
// - Speed: Inline tt1_calcInsIndex
// - Speed: Store ptr to current note in RAM instead of reconstructing it?
//       Might also save the need for cur_note_index


/* =====================================================================
 * Modified generic player, with song data pointer instead of fixed
 * functions and conditional compilation.
 * Replace tt1_trackdata.k65 template in TIATracker/player/k65 with the
 * provided file.
 * =====================================================================
 */

// song field offsets in structure
[
    TT_INSCTRLTABLE         = 0
    TT_INSADINDEXES         = 2
    TT_INSSUSTAININDEXES    = 4
    TT_INSRELEASEINDEXES    = 6
    TT_INSFREQVOLTABLE      = 8
    TT_PERCINDEXES          = 10
    TT_PERCFREQTABLE        = 12
    TT_PERCCTRLVOLTABLE     = 14
    TT_PATTERNSPEEDS        = 16
    TT_SEQUENCETABLE        = 18
    TT_PATTERNPTRLO         = 20
    TT_PATTERNPTRHI         = 22
    TT_META                 = 24
    TT_PTR_SZ               = 26
    // 1: Global song speed, 0: Each pattern has individual speed
    TT_GLOBAL_SPEED         = 0+TT_PTR_SZ
    // duration (number of TV frames) of a note
    TT_SPEED                = 1+TT_PTR_SZ
    // duration of odd frames (needs TT_USE_FUNKTEMPO)
    TT_ODD_SPEED            = 2+TT_PTR_SZ
    // 1: Odd/even rows have different SPEED values, +7 bytes
    TT_USE_FUNKTEMPO        = 3+TT_PTR_SZ
    // first pattern index
    TT_C0INIT               = 4+TT_PTR_SZ
    TT_C1INIT               = 5+TT_PTR_SZ
]

[
    TT_FREQ_MASK    = 31,
    TT_INS_HOLD     = 8,
    TT_INS_PAUSE    = 16
]

/* =====================================================================
 * Helper function: Retrieves current note. May advance pattern if needed.
 * =====================================================================
 */

inline tt1_getSequenceEntry {
.constructPatPtr:
    y=tt_cur_pat_index,x
    a=(tt_ptrtab+TT_SEQUENCETABLE),y <0{
        a&0b01111111 tt_cur_pat_index,x=a >=0goto .constructPatPtr
    }
}

inline tt1_getCurNoteIndex {
    // If the V flag is set and if the new note is an instrument,
    // it means it got pre-fetched by an overlay percussion, it has
    // to remain in sustain.
    o-
    // check if note had been pre-fetched by overlay perc already
    a=tt_cur_note_index,x <0{
        // If so, remove flag
       a&0b01111111 tt_cur_note_index,x=a
       // Set V flag for later
       a&?tt1_calcInsIndex
    }
    y=a
}

func tt1_fetchCurrentNote {
    // Construct pattern pointer
.constructPatPtr:
    tt1_getSequenceEntry
    y=a tt_ptr=a=(tt_ptrtab+TT_PATTERNPTRLO),y tt_ptr+1=a=(tt_ptrtab+TT_PATTERNPTRHI),y
    // Get new note
    tt1_getCurNoteIndex a=(tt_ptr),y
    // Pre-process new note
    // 7..5: instrument (1..7), 4..0 (0..31): frequency
    // 0/0: End of pattern
    =={
        // End of pattern: Advance to next pattern
        tt_cur_note_index,x=a       // a is 0
        tt_cur_pat_index,x++
        !=goto .constructPatPtr     //unconditional
    }
}

/* =====================================================================
 * Helper subroutine to minimize ROM footprint.
 * =====================================================================
 */
func tt1_calcInsIndex {
    // move upper 3 bits to lower 3
    // The lsr opcode has bit 6 set and thus is used for a "bit"
    // instruction in the player!
    a>> a>> a>> a>> a>>
    y=a
}


/* =====================================================================
 * Sequencer
 * =====================================================================
 */

inline tt1_localSetTimer {
    tt_ptr=y y=TT_USE_FUNKTEMPO a=(tt_song),y y=tt_ptr a?0
    !={
        // Test for odd/even frame
        a=tt_cur_note_index[0] a>>
        a=(tt_ptrtab+TT_PATTERNSPEEDS),y            // does not affect carry flag
        <goto .evenFrame
        a&0x0f                          // does not affect carry flag
        >=goto .storeFunkTempo          // unconditional
    .evenFrame:
        a>> a>> a>> a>>
    .storeFunkTempo:
        tt_timer=a
        goto .done
    }
    tt_timer=a=(tt_ptrtab+TT_PATTERNSPEEDS),y
.done:
}

inline tt1_globalGetNextTimerValue {
    y=TT_USE_FUNKTEMPO a=(tt_song),y
    != { a=tt_cur_note_index[0] a>> >={ y=TT_ODD_SPEED x=a=(tt_song),y } }
}

inline tt1_setTimerValue {
    y=TT_GLOBAL_SPEED a=(tt_song),y
    !={
        y++ x=a=(tt_song),y
        tt1_globalGetNextTimerValue
        tt_timer=x
        goto .done
    }
    // Get current pattern index now in y
    y=tt_cur_pat_index a=(tt_ptrtab+TT_SEQUENCETABLE),y y=a
    // Set timer depending on TT_USE_FUNKTEMPO
    tt1_localSetTimer
.done:
}

inline tt1_slideHoldSwitch {
    ==goto tt1_pause
    >=goto tt1_newNote
    // --- slide/hold ---
    // Adjust frequency and hold note in sustain.
    // composer/tracker has to make sure that no unwanted
    // under/overflow happens.
    // Note: f = f + (8-(16-x)) = x + f - 8
    a+tt_cur_ins,x c+ a-8 tt_cur_ins,x=a
    >=goto tt1_finishedNewNote
}

inline tt1_checkForOverlay {
    // If V flag is set, this note had been pre-fetched. That means
    // it should remain in sustain.
    <<=goto tt1_finishedNewNote
}

inline tt1_sequencer {
    // Decrease speed timer
    tt_timer-- <0 goto .update goto .done
.update:
        // Timer ran out: Advance to next note for each channel
        x=1 {
            tt1_fetchCurrentNote
            // Parse new note from pattern
            a?[TT_INS_PAUSE] tt1_slideHoldSwitch

            // --- pause ---
tt1_pause:
            // Get release index for current instrument. Since a pause can
            // only follow an instrument, we don't need to handle percussion
            // or commands.
            a=tt_cur_ins,x tt1_calcInsIndex
            a=(tt_ptrtab+TT_INSRELEASEINDEXES),y      // -1 b/c instruments start at #1
            // Put it into release. Skip junk byte so index no longer indicates
            // sustain phase.
            c- a+1 <goto tt1_storeADIndex    // unconditional

            // --- start instrument or percussion ---
tt1_newNote:
            tt_cur_ins,x=a                  // set new instrument
            // Instrument or percussion?
            a?[TT_FREQ_MASK+1] >=goto tt1_startInstrument

            // --- start percussion ---
            // Get index of envelope
            y=a
            // -17 because percussion start with 17
            a=(tt_ptrtab+TT_PERCINDEXES),y
            !=goto tt1_storeADIndex          // unconditional, since index values are >0

            // --- start instrument ---
tt1_startInstrument:
            tt1_checkForOverlay
            // Put note into attack/decay
            tt1_calcInsIndex
            a=(tt_ptrtab+TT_INSADINDEXES),y           // -1 because instruments start at #1
tt1_storeADIndex:
            tt_envelope_index,x=a

            // --- Finished parsing new note ---
tt1_finishedNewNote:
            // increase note index into pattern
            tt_cur_note_index,x++

            // Next channel
            x--
        }>=0

        // Reset timer
        tt1_setTimerValue // absolute jmps take 1 byte less than func
.done:
}


/* =====================================================================
 * Update registers
 * =====================================================================
 */

inline tt1_checkForStartsWithNotes {
    // This branch can be removed if track starts with a note in each channel
    ==goto tt1_afterAudioUpdate
}

inline tt1_doOverlay {
	>=0goto tt1_afterAudioUpdate
	// Overlay percussion: Fetch next note out of order
	tt1_fetchCurrentNote
	// Only do something if it's a melodic instrument
	a?[TT_FREQ_MASK+1]
	<goto tt1_afterAudioUpdate
	// Instrument: Put into sustain
	tt_cur_ins,x=a				// set new instrument
	tt1_calcInsIndex
	tt_envelope_index,x=a=(tt_ptrtab+TT_INSSUSTAININDEXES),y
	// Set prefetch flag. asl-sec-ror is smaller than lda-ora #128-sta
	tt_cur_note_index,x<< c+ tt_cur_note_index,x>>>
	<0goto tt1_afterAudioUpdate
}

inline tt1_updateRegisters {
    x=1 {
        // Percussion or melodic instrument?
        a=tt_cur_ins,x tt1_checkForStartsWithNotes
        a?[TT_FREQ_MASK+1] >=goto tt1_instrument

        // --- Percussion: Get envelope index ---
        y=tt_envelope_index,x
        // Set AUDC and AUDV value from envelope
        a=(tt_ptrtab+TT_PERCCTRLVOLTABLE),y       // -1 because values are stored +1
        !={ tt_envelope_index,x++ }     // 0 means end of percussion data
        AUDV0s,x=a a>> a>> a>> a>> AUDC0s,x=a
        // Set AUDF
        a=(tt_ptrtab+TT_PERCFREQTABLE),y			// -1 because values are stored +1
		// Bit 7 (overlay) might be set, but is unused in AUDF
		AUDF0s,x=a
		tt1_doOverlay

tt1_instrument:
        // --- Melodic instrument ---
        // Compute index into ADSR indexes and master Ctrl tables
		tt1_calcInsIndex
        // Set AUDC with master value for this instrument, while we are at it
		AUDC0s,x=a=(tt_ptrtab+TT_INSCTRLTABLE),y
        // advance ADSR counter and compare to end of Sustain
		a=tt_envelope_index,x a?(tt_ptrtab+TT_INSRELEASEINDEXES),y =={ a=(tt_ptrtab+TT_INSSUSTAININDEXES),y }
		y=a
        // Set volume from envelope
		a=(tt_ptrtab+TT_INSFREQVOLTABLE),y !={ y++ }	// 0 means end of release has been reached
		tt_envelope_index,x=y
        AUDV0s,x=a
        // Now adjust frequency with ADSR value from envelope
		a>> a>> a>> a>> c- a+tt_cur_ins,x c+ a-8
        AUDF0s,x=a

tt1_afterAudioUpdate:
        // Next channel
        x--
    }>=0
}

inline tt1_setptr
{
    y=TT_PTR_SZ-1 { a=(tt_song),y tt_ptrtab,y=a y-- }>=0
}

inline tt1_init_song
{
    tt_timer=a=0
    y=TT_C0INIT tt_cur_pat_index=a=(tt_song),y y++ tt_cur_pat_index+1=a=(tt_song),y
}

naked tt1_player_back {
    address 0xffe9
    a&?bank_core
}
naked tt1_player {
    address 0xf403
    tt_song=a=songs1_lo,y tt_song+1=a=songs1_hi,y
    a&?tt_timer <0{ tt1_init_song }
    tt1_setptr
    tt1_sequencer
    tt1_updateRegisters
    goto tt1_player_back
}
